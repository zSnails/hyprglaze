const std = @import("std");
const hypr = @import("hypr.zig");

const c = @cImport({
    @cInclude("wayland-client.h");
    @cInclude("wayland-egl.h");
    @cInclude("xdg-shell-client-protocol.h");
    @cInclude("wlr-layer-shell-unstable-v1-client-protocol.h");
});

const log = std.log.scoped(.wayland_state);

pub const WaylandState = struct {
    display: *c.wl_display,
    registry: *c.wl_registry,
    compositor: ?*c.wl_compositor = null,
    layer_shell: ?*c.zwlr_layer_shell_v1 = null,
    surface: ?*c.wl_surface = null,
    layer_surface: ?*c.zwlr_layer_surface_v1 = null,
    egl_window: ?*c.wl_egl_window = null,
    allocator: std.mem.Allocator,
    monitors: std.ArrayList(hypr.MonitorInfo),

    configured: bool = false,
    width: u31 = 0,
    height: u31 = 0,
    should_close: bool = false,
    frame_done: bool = true,
    resize_pending: bool = false,
    must_use_monitor: ?[]const u8 = null,

    /// Two-phase init: `self` must be caller-owned storage that outlives
    /// the connection, because its address is registered as listener
    /// userdata — registry events (e.g. monitor hotplug) arrive through
    /// that pointer for the lifetime of the display.
    pub fn init(self: *WaylandState, allocator: std.mem.Allocator, monitor_name: ?[]const u8) !void {
        const display = c.wl_display_connect(null) orelse return error.DisplayConnectFailed;
        errdefer c.wl_display_disconnect(display);
        const registry = c.wl_display_get_registry(display) orelse return error.RegistryFailed;
        errdefer c.wl_registry_destroy(registry);

        var monitors = try std.ArrayList(hypr.MonitorInfo).initCapacity(allocator, 2);
        errdefer monitors.deinit(allocator);

        self.* = .{
            .display = display,
            .registry = registry,
            .allocator = allocator,
            .monitors = monitors,
            .must_use_monitor = monitor_name,
        };

        if (c.wl_registry_add_listener(registry, &registry_listener, self) != 0)
            return error.RegistryListenerFailed;

        // Round-trip to get globals
        if (c.wl_display_roundtrip(display) == -1) return error.RoundtripFailed;

        // NOTE: this roundtrip is needed, the previous one gets the globals,
        // this one gets the output display geometry, name, mode, etc. It has
        // to stay.
        if (c.wl_display_roundtrip(display) == -1) return error.RoundtripFailed;

        if (self.compositor == null) return error.NoCompositor;
        if (self.layer_shell == null) return error.NoLayerShell;
    }

    fn getMonitor(self: *WaylandState) ?*c.wl_output {
        if (self.must_use_monitor) |must_use_monitor| {
            for (self.monitors.items) |mon| {
                if (std.mem.eql(u8, mon.name, must_use_monitor)) {
                    return @ptrCast(mon.output);
                }
            }
        }
        return null;
    }

    pub fn createLayerSurface(self: *WaylandState) !void {
        self.surface = c.wl_compositor_create_surface(self.compositor) orelse
            return error.SurfaceCreateFailed;

        const monitor = self.getMonitor();

        self.layer_surface = c.zwlr_layer_shell_v1_get_layer_surface(
            self.layer_shell,
            self.surface,
            monitor,
            c.ZWLR_LAYER_SHELL_V1_LAYER_BACKGROUND,
            "hyprglaze",
        ) orelse return error.LayerSurfaceCreateFailed;

        // Anchor to all four edges = fullscreen
        c.zwlr_layer_surface_v1_set_anchor(self.layer_surface, c.ZWLR_LAYER_SURFACE_V1_ANCHOR_TOP |
            c.ZWLR_LAYER_SURFACE_V1_ANCHOR_BOTTOM |
            c.ZWLR_LAYER_SURFACE_V1_ANCHOR_LEFT |
            c.ZWLR_LAYER_SURFACE_V1_ANCHOR_RIGHT);

        // Exclusive zone -1: ignore other exclusive zones
        c.zwlr_layer_surface_v1_set_exclusive_zone(self.layer_surface, -1);

        // No keyboard interactivity
        c.zwlr_layer_surface_v1_set_keyboard_interactivity(self.layer_surface, c.ZWLR_LAYER_SURFACE_V1_KEYBOARD_INTERACTIVITY_NONE);

        if (c.zwlr_layer_surface_v1_add_listener(self.layer_surface, &layer_surface_listener, self) != 0)
            return error.LayerSurfaceListenerFailed;

        // Initial commit to trigger configure
        c.wl_surface_commit(self.surface);

        // Round-trip to get configure event
        if (c.wl_display_roundtrip(self.display) == -1) return error.RoundtripFailed;
    }

    pub fn createEglWindow(self: *WaylandState) !void {
        if (self.width == 0 or self.height == 0) return error.NotConfigured;

        if (self.egl_window) |win| {
            c.wl_egl_window_resize(win, self.width, self.height, 0, 0);
        } else {
            self.egl_window = c.wl_egl_window_create(self.surface, self.width, self.height) orelse
                return error.EglWindowCreateFailed;
        }
    }

    pub fn requestFrame(self: *WaylandState) !void {
        const callback = c.wl_surface_frame(self.surface) orelse return error.FrameCallbackFailed;
        if (c.wl_callback_add_listener(callback, &frame_listener, self) != 0)
            return error.FrameListenerFailed;
        self.frame_done = false;
    }

    pub fn dispatch(self: *WaylandState) !void {
        if (c.wl_display_dispatch(self.display) == -1) return error.DispatchFailed;
    }

    /// Tear down Wayland-side resources and reconnect to the compositor.
    /// On success, a fresh display/registry/compositor/layer_shell/surface/egl_window
    /// is live. Caller must recreate EGL state and re-upload GL resources since the
    /// EGL display was bound to the previous wl_display pointer.
    pub fn reconnect(self: *WaylandState) !void {
        if (self.egl_window) |win| c.wl_egl_window_destroy(win);
        if (self.layer_surface) |ls| c.zwlr_layer_surface_v1_destroy(ls);
        if (self.surface) |s| c.wl_surface_destroy(s);
        c.wl_registry_destroy(self.registry);
        c.wl_display_disconnect(self.display);

        const display = c.wl_display_connect(null) orelse return error.DisplayConnectFailed;
        const registry = c.wl_display_get_registry(display) orelse return error.RegistryFailed;

        self.* = .{
            .display = display,
            .registry = registry,
            .allocator = self.allocator,
            .monitors = self.monitors,
        };

        if (c.wl_registry_add_listener(registry, &registry_listener, self) != 0)
            return error.RegistryListenerFailed;
        if (c.wl_display_roundtrip(display) == -1) return error.RoundtripFailed;
        if (self.compositor == null) return error.NoCompositor;
        if (self.layer_shell == null) return error.NoLayerShell;

        try self.createLayerSurface();
        if (!self.configured) return error.NotConfigured;
        try self.createEglWindow();
    }

    pub fn deinit(self: *WaylandState) void {
        if (self.egl_window) |win| c.wl_egl_window_destroy(win);
        if (self.layer_surface) |ls| c.zwlr_layer_surface_v1_destroy(ls);
        if (self.surface) |s| c.wl_surface_destroy(s);
        self.monitors.deinit(self.allocator);
        c.wl_registry_destroy(self.registry);
        c.wl_display_disconnect(self.display);
    }
};

// --- Registry listener ---

const registry_listener = c.wl_registry_listener{
    .global = registryGlobal,
    .global_remove = registryGlobalRemove,
};

fn registryGlobal(
    data: ?*anyopaque,
    registry: ?*c.wl_registry,
    name: u32,
    interface: ?[*:0]const u8,
    version: u32,
) callconv(.c) void {
    const state: *WaylandState = @ptrCast(@alignCast(data));
    const iface = std.mem.span(interface orelse return);

    if (std.mem.eql(u8, iface, "wl_compositor")) {
        state.compositor = @ptrCast(c.wl_registry_bind(registry, name, &c.wl_compositor_interface, @min(version, 4)));
    } else if (std.mem.eql(u8, iface, "zwlr_layer_shell_v1")) {
        state.layer_shell = @ptrCast(c.wl_registry_bind(registry, name, &c.zwlr_layer_shell_v1_interface, @min(version, 1)));
    } else if (std.mem.eql(u8, iface, "wl_output")) {
        // NOTE: I'm not storing this output, this output is the
        const output: *c.wl_output = @ptrCast(c.wl_registry_bind(registry, name, &c.wl_output_interface, @min(version, 4)));
        if (c.wl_output_add_listener(output, &output_listener, data) != 0) {
            log.err("something went wrong when registering the output listener, a default output will be used if available", .{});
        }
    }
}

fn registryGlobalRemove(_: ?*anyopaque, _: ?*c.wl_registry, _: u32) callconv(.c) void {}

// --- Output listener --

var temp_monitor: hypr.MonitorInfo = undefined;

fn outputHandleGeometry(_: ?*anyopaque, _: ?*c.wl_output, x: i32, y: i32, _: i32, _: i32, _: i32, _: [*c]const u8, _: [*c]const u8, _: i32) callconv(.c) void {
    temp_monitor.x = x;
    temp_monitor.y = y;
}

fn outputHandleMode(_: ?*anyopaque, _: ?*c.wl_output, _: u32, width: i32, height: i32, _: i32) callconv(.c) void {
    temp_monitor.width = width;
    temp_monitor.height = height;
}

fn outputHandleDone(data: ?*anyopaque, output: ?*c.wl_output) callconv(.c) void {
    const state: *WaylandState = @ptrCast(@alignCast(data));
    temp_monitor.output = @ptrCast(output);
    state.monitors.append(state.allocator, temp_monitor) catch unreachable;
}

fn outputHandleScale(_: ?*anyopaque, _: ?*c.wl_output, scale: i32) callconv(.c) void {
    temp_monitor.scale = @floatFromInt(scale);
}

fn outputHandleName(_: ?*anyopaque, _: ?*c.wl_output, name: [*c]const u8) callconv(.c) void {
    temp_monitor.name = std.mem.span(name);
}

fn outputHandleDescription(_: ?*anyopaque, _: ?*c.wl_output, _: [*c]const u8) callconv(.c) void {}

const output_listener = c.wl_output_listener{
    .geometry = outputHandleGeometry,
    .mode = outputHandleMode,
    .done = outputHandleDone,
    .scale = outputHandleScale,
    .name = outputHandleName,
    .description = outputHandleDescription,
};

// --- Layer surface listener ---

const layer_surface_listener = c.zwlr_layer_surface_v1_listener{
    .configure = layerSurfaceConfigure,
    .closed = layerSurfaceClosed,
};

fn layerSurfaceConfigure(
    data: ?*anyopaque,
    surface: ?*c.zwlr_layer_surface_v1,
    serial: u32,
    width: u32,
    height: u32,
) callconv(.c) void {
    const state: *WaylandState = @ptrCast(@alignCast(data));

    const new_w: u31 = @intCast(width);
    const new_h: u31 = @intCast(height);
    if (state.configured and (new_w != state.width or new_h != state.height)) {
        if (state.egl_window) |win| c.wl_egl_window_resize(win, new_w, new_h, 0, 0);
        state.resize_pending = true;
    }
    state.width = new_w;
    state.height = new_h;
    state.configured = true;

    c.zwlr_layer_surface_v1_ack_configure(surface, serial);
}

fn layerSurfaceClosed(data: ?*anyopaque, _: ?*c.zwlr_layer_surface_v1) callconv(.c) void {
    const state: *WaylandState = @ptrCast(@alignCast(data));
    state.should_close = true;
}

// --- Frame callback listener ---

const frame_listener = c.wl_callback_listener{
    .done = frameDone,
};

fn frameDone(data: ?*anyopaque, callback: ?*c.wl_callback, _: u32) callconv(.c) void {
    const state: *WaylandState = @ptrCast(@alignCast(data));
    state.frame_done = true;
    if (callback) |cb| c.wl_callback_destroy(cb);
}
